#!ipxe

:start
set server_ip __IP__
set mint_path /srv/nfs/mint
set mint_args root=/dev/nfs boot=casper netboot=nfs nfsroot=${server_ip}:${mint_path} ip=dhcp  username=live_user hostname=linux-repair-cafe systemd.default_device_timeout_sec=5

set mint_kernel nfs://${server_ip}${mint_path}/casper/vmlinuz
set mint_initrd nfs://${server_ip}${mint_path}__INITRD__

:menu-install
menu Linux repair cafe boot options
item --key l mint-live        Start __DESCRIPTION__
item --key c mint-live_compat Start __DESCRIPTION__ (compatibility mode)
item --key i mint-install     Repair Cafe automated OEM install, NO QUESTIONS - disk overwritten
item
item shell                Drop to iPXE shell
item reboot               Reboot computer
item --key x exit         Exit iPXE and continue BIOS boot
choose selected && goto ${selected} || void


:mint-live
echo Start __DESCRIPTION__
kernel ${mint_kernel}
initrd ${mint_initrd}
imgargs vmlinuz ${mint_args} quiet splash
boot

:mint-live_compat
echo Start __DESCRIPTION__ (compatibility mode)
kernel ${mint_kernel}
initrd ${mint_initrd}
imgargs vmlinuz ${mint_args} noapic noacpi nosplash irqpoll nomodeset
boot

:mint-install
echo Install __DESCRIPTION__
kernel ${mint_kernel}
initrd ${mint_initrd}
imgargs vmlinuz ${mint_args} -- file=/cdrom/preseed/seed/linuxmint_custom.seed auto noprompt automatic-ubiquity
boot

:shell
echo Type 'exit' to get the back to the menu
shell
set menu-timeout 0
set submenu-timeout 0
goto start

:failed
echo Booting failed, dropping to shell
goto shell

:reboot
reboot

:exit
exit

:config
config
goto start
